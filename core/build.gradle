import com.chrynan.chords.buildSrc.LibraryConstants

apply plugin: "kotlin-multiplatform"
apply plugin: "kotlinx-serialization"

group LibraryConstants.group
version LibraryConstants.versionName

kotlin {
    jvm()
    js {
        browser {
        }
    }
    ios()

    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"

                api "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.0"

                api "com.chrynan.colors:colors-core:0.2.5"
            }
        }
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
            }
        }
        jvmTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-junit"
            }
        }
        jsTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-js"
            }
        }
        all {
            languageSettings.enableLanguageFeature("InlineClasses")
            languageSettings.useExperimentalAnnotation("kotlin.RequiresOptIn")
        }
    }
}

configurations {
    compileClasspath
}

publishing {
    publications {
        kotlinMultiplatform {
            artifactId = "chords-core"
        }
        jvm {
            artifactId = "chords-core-jvm"
        }
        js {
            artifactId = "chords-core-js"
        }
        iosArm64 {
            artifactId = "chords-core-ios-arm64"
        }
        iosX64 {
            artifactId = "chords-core-ios-x64"
        }
        metadata {
            artifactId = "chords-core-metadata"
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}
